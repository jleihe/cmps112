#!/usr/bin/perl
# $Id: getpwnam,v 1.6 2014-10-09 17:28:20-07 - - $
#
# NAME
#    getpwnam - get password file entries
#
# SYNOPSIS
#    getpwnam [-p] [username...]
#
# OPTIONS
#    -p Print entries in the same format as /etc/passwd isntead of
#       the default more verbose output.
#
# DESCRIPTION
#    Uses getpwnam to obtain password file entries and prints the
#    information.  Uses the current user of none are specified.
#

use strict;
use warnings;
$0 =~ s|.*/||;

use Getopt::Std;
my %opts;
getopts "p", \%opts;

push @ARGV, scalar getpwuid $< unless @ARGV;
for my $argvname (@ARGV) {
   my ($username, $passwd, $uid, $gid, $quota, $comment, $gcos,
       $homedir, $shell, $expire) = getpwnam $argvname;
   warn "$0: $argvname: No such username\n" and next unless $username;
   if ($opts{"p"}) {
      print "$username:$passwd:$uid:$gid:$gcos:$homedir:$shell\n";
   }else {
      print "$argvname:\n";
      print "   username = $username\n";
      print "   passwd   = $passwd\n";
      print "   uid      = $uid\n";
      print "   gid      = $gid\n";
      print "   quota    = $quota\n" if defined $quota;
      print "   comment  = $comment\n" if defined $comment;
      print "   gcos     = $gcos\n";
      print "   homedir  = $homedir\n";
      print "   shell    = $shell\n";
      print "   expire   = $expire\n" if defined $expire;
   }
}

