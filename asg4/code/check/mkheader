#!/usr/bin/perl
# $Id: mkheader,v 1.130 2014-05-29 18:42:23-07 - - $
#
# NAME
#    mkheader - make header for root of course directory
#    
# SYNOPSIS
#    mkheader
#
# DESCRIPTION
#    Reads in the file README.cat and tries to detect all due dates
#    with leading text of the form Ddd Mmm Nn, sorts them into order
#    by date, assuming the current year and makes a header file
#    HEADER.html to appear ahead of the default directory listing.
#    Output in the current directory.
#

BEGIN {
   push @INC, "/afs/cats.ucsc.edu/courses/cmps012b-wm/usr/share/perl5"
}

use strict;
use warnings;
use Getopt::Std;
use HTTP::Date qw (str2time);
use POSIX qw (strftime);

my $status = 0;
$SIG{'__WARN__'} = sub {print STDERR @_; $status = 1; };
$SIG{'__DIE__'} = sub {warn @_; exit 1; };

my $daylen = 24 * 60 * 60;
my %months = qw (Jan 0 Feb 1 Mar 2 Apr 3 May 4 Jun 5
                 Jul 6 Aug 7 Sep 8 Oct 9 Nov 10 Dec 11);
my $monthpat = join "|", keys %months;
my $daypat = join "|", qw (Sun Mon Tue Wed Thu Fri Sat);
my $datepat = qr {(\.\s+($daypat)\s+($monthpat)\s+(\d+)\.)(.*)};
my $timenow = $^T;
my @localtime_now = localtime $timenow;
my $year = $localtime_now[5] + 1900;

$0 =~ s|.*/||;
@ARGV == 0 or die "Usage: $0\n";

my $coursepwd = "/afs/cats.ucsc.edu/courses";
my $pwd = `pwd`;
chomp $pwd;
my $url = `pwd2url`;
chomp $url;
my $TITLE = $pwd;
$TITLE =~ s|/afs/cats.ucsc.edu/courses/|~|;
print "$0: $TITLE\n";

my $bg_data_color = "#0F3F3F";
my $bg_empty_color = "#2F2F2F";
my $fg_hover_color = "#FF2F2F";
my $fg_link_color = "#2FFFFF";
my $fg_text_color = "#2FFF2F";
my $fg_visited_color = "#FFFF2F";

sub print_html_head ($;$) {
   my ($htmlfile, $calendar_table) = @_;

   print $htmlfile "<HEAD>\n";
   print $htmlfile "<TITLE>$TITLE</TITLE>\n";
   print $htmlfile "<STYLE>\n";
   print $htmlfile "body {\n";
   print $htmlfile "   color: $fg_text_color !important;\n";
   print $htmlfile "   background-color: $bg_empty_color !important;\n";
   print $htmlfile "}\n";
   if ($calendar_table) {
      print $htmlfile "table {\n";
      print $htmlfile "   border: 2px solid;\n";
      print $htmlfile "   border-spacing: 0px;\n";
      print $htmlfile "}\n";
      print $htmlfile "td {\n";
      print $htmlfile "   border: 2px solid;\n";
      print $htmlfile "   padding: 3px;\n";
      print $htmlfile "   width: 14%;\n";
      print $htmlfile "   vertical-align: top;\n";
      print $htmlfile "}\n";
   }
   print $htmlfile "A:link {color: $fg_link_color}\n";
   print $htmlfile "A:visited {color: $fg_visited_color}\n";
   print $htmlfile "A:hover {color: $fg_hover_color}\n";
   print $htmlfile "</STYLE>\n";
   print $htmlfile "</HEAD>\n";

}

my $readmename = "README.cat";
my $readme_opened = open README, "<$readmename";
unless (defined $readme_opened) {
   my $headerhtml = "HEADER.html";
   open my $HDRHTML, ">$headerhtml" or die "$0: $headerhtml: $!\n";
   print_html_head $HDRHTML;
   print $HDRHTML "<BODY>\n";
   print $HDRHTML "<BIG><B><TT>\n";
   print $HDRHTML "<A HREF=$url>$url</A>\n";
   print $HDRHTML "<BR>$pwd\n";
   print $HDRHTML "</TT></B></BIG>\n";
   close $HDRHTML;
   exit;
}

my %duedates;
my $rcsid = "";
my $quarter = "";
my @registrar;
my $first_time = 1e1000;
my $last_time = 0;
my %calendar;
while (defined (my $line = <README>)) {
   $rcsid = $& and next if $line =~ m/\$(Id): .*?\$/;
   $quarter = $1 and next if $line =~ m/\.\s+Quarter:\s+(.*)/;
   push @registrar, $& and next if $line =~ m|http://registrar\.\S+|;
   next unless $line =~ m/$datepat/;
   my ($date, $wday, $month, $mday, $item) = ($1, $2, $3, $4, $5);
   my $duedate = "$month $mday $year";
   my $duetime = str2time $duedate;
   $first_time = $duetime if $first_time > $duetime;
   $last_time = $duetime if $last_time < $duetime;
   print "$duedate - not a valid date\n" and next unless $duetime;
   my @localduetime = localtime $duetime;
   my $chkwday = strftime "%a", @localduetime;
   if ($wday ne $chkwday) {
      $duedates{$date} = "$date is really $chkwday";
      warn "$duedates{$date}\n";
   }
   my $duetimekey = strftime "%Y-%m-%d", @localduetime;
   $duedates{$duetimekey} = "$date$item";
   my $weeknum = strftime "%U", @localduetime;
   my $daynum = strftime "%w", @localduetime;
   my $caldate = strftime "%a %b %d", @localduetime;
   if (defined $calendar{$weeknum}{$daynum}) {
      warn "\$calendar{$weeknum}{$daynum}:\n";
      warn "Defined:   [@{$calendar{$weeknum}{$daynum}}]\n";
      warn "Redefined: [$caldate $item]\n";
   }else {
      $calendar{$weeknum}{$daynum} = [$caldate, $item];
   }
}
close README;
exit $status if $status;

print "$0: quarter $quarter\n";

$first_time -= $daylen while $timenow < $first_time
                         and $first_time <= $last_time;
for (my $time = $first_time; $time <= $last_time; $time += $daylen) {
   my @gmtime = gmtime $time;
   my $week = strftime "%U", @gmtime;
   my $day = strftime "%w", @gmtime;
   my $caldate = strftime "%a %b %d", @gmtime;
   $calendar{$week}{$day} = [$caldate] unless $calendar{$week}{$day};
}

$rcsid =~ s/\$(Id): (.*?)(,v(\s+\S+){3}).*/$2$3/;

sub urlify($) {
   my ($line) = @_;
   $line =~ s|http:([^,\s])+|<A HREF=$&>$&</A>|
            unless $line =~ m|http:\S+\*|;
   return $line;
}

my $headerhtml = "HEADER.html";
open my $HDRHTML, ">$headerhtml" or die "$0: $headerhtml: $!\n";
my $headercat = "HEADER.cat";
open my $HDRCAT, ">$headercat" or die "$0: $headercat: $!\n";

sub print_both($) {
   my ($line) = @_;
   print $HDRHTML urlify $line;
   $line =~ s/<.*?>//g;
   print $HDRCAT $line;
}

sub ten_chars($) {
   my ($chars) = @_;
   $chars = substr $chars . " " x 10, 0, 10;
   return $chars;
}

print_html_head $HDRHTML, 1;
print $HDRHTML "<BODY>\n";
print_both     "<B><BIG><TT>\n";
print_both     "$url\n";
print_both     "<BR>$pwd\n";
print_both     "<PRE>\n";
print_both     "$duedates{$_}\n" for sort keys %duedates;
print_both     "</PRE>\n";
print $HDRCAT  "HEADER: HEADER.cat\n";
print $HDRHTML urlify "HEADER: http:HEADER.html\n";
print $HDRCAT  "README: $rcsid\n";
$rcsid =~ s/\.txt,v/.html,v/;
print $HDRHTML urlify "<BR>README: http:$rcsid\n";
print_both     "<BR>REGISTRAR: $_\n" for @registrar;
print_both     "<P>\n";
print $HDRHTML "</TT></BIG></B>\n";

if ($first_time <= $last_time) {
   print $HDRHTML "<TT>\n";
   print $HDRHTML "<TABLE>\n";
   for my $weeknum (sort keys %calendar) {
      print $HDRHTML "<TR>\n";
      for my $daynum (0..6) {
         my $data = $calendar{$weeknum}{$daynum};
         my $htline = "&nbsp;<BR>&nbsp;";
         my $td_style = "";
         if (defined $data) {
            my ($date, $info) = @$data;
            $date =~ s/\s/&nbsp;/g;
            $date =~ s/&nbsp;0/&nbsp;&nbsp;/;
            if (not defined $info) {
               $htline = "$date<BR>&nbsp;";
            }else {
               $info =~ m/^\s*(.*?)\.\s*(http:\S+)?/;
               my ($item, $url) = (ten_chars $1, $2);
               $item =~ s/\s/&nbsp;/g;
               $htline = "\n<B>$date\n<BR>$item</B>";
               $htline = "\n<A HREF=$url>$htline</A>"
                       if $url and $url !~ m/\*$/;
               $td_style = "background-color: $bg_data_color;";
            }
         }
         print $HDRHTML "<TD style=\"$td_style\">$htline\n</TD>\n";
      }
      print $HDRHTML "</TR>\n";
   }
   print $HDRHTML "</TABLE>\n";
}
print $HDRHTML "</BODY>\n";

if ($first_time <= $last_time) {
   sub print_sep_line { print $HDRCAT "+----------" x 7, "+\n" }
   for my $weeknum (sort keys %calendar) {
      print_sep_line;
      my $week = $calendar {$weeknum};
      for my $row (0..1) {
         for my $daynum (0..6) {
            my $day = $week->{$daynum};
            my $data = defined ($day) && defined ($day->[$row])
                     ? $day->[$row] : "";
            $data =~ s/^\s*(.*?)\..*/$1/;
            $data =~ s/ 0(\d)/  $1/;
            print $HDRCAT "|", ten_chars $data;
         }
         print $HDRCAT "|\n";
      }
   }
   print_sep_line;
}

close $HDRHTML;
close $HDRCAT;

system "ls -la $headerhtml $headercat";
system "cat $headercat";

